buildscript {
    ext['dependencyPluginVersion'] = '0.6.0.RELEASE'
    ext['springCloudVersion'] = 'Brixton.RELEASE'
    ext['springBootVersion'] = '1.3.5.RELEASE'
    ext['springVersion'] = '4.2.5.RELEASE'
    ext['gradleDockerVersion'] = '1.2'
    ext['junitVersion'] = '4.10'
    ext['embedMongoVersion'] = '1.50.0'
    ext['restAssuredVersion'] = '2.9.0'
    ext['guavaVersion'] = '18.0'
    ext['commonsLangVersion'] = '3.4'
    ext['slf4jVersion'] = '1.7.12'
    ext['logbackVersion'] = '1.1.7'
    ext['lombokVersion'] = '1.16.20'
    ext['modelMapperVersion'] = '1.1.0'
    ext['owaspVersion'] = '2.0.1'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyPluginVersion}"
        classpath "se.transmode.gradle:gradle-docker:${gradleDockerVersion}"
        classpath "org.owasp.esapi:esapi:${owaspVersion}"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.1.0'
    }

}

group = 'imc-test'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'docker'
apply plugin: 'com.moowork.node'
apply plugin: 'io.spring.dependency-management'

jar {
    group = 'imc-test'
    baseName = 'rock-paper-scissors'
    version =  '1.0'
    manifest {
        attributes 'Main-Class': 'com.imc.rps.Application'
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
    compile.exclude module : 'spring-boot-starter-logging'
//    all*.exclude group: 'ch.qos.logback', module: 'logback-core'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // spring boot
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-hateoas'
    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compile 'org.springframework.boot:spring-boot-starter-cache'

    // spring cloud
    compile 'org.springframework.cloud:spring-cloud-starter-hystrix'
    compile 'org.springframework.cloud:spring-cloud-starter-eureka'

    // springfox/swagger
//    compile "io.springfox:springfox-swagger2"
//    compile "io.springfox:springfox-swagger-ui"
//    testCompile "io.github.robwin:swagger2markup:0.9.2"

    // guava
    compile "com.google.guava:guava:${guavaVersion}"

    // logging
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    // apache commons lang
    compile "org.apache.commons:commons-lang3:${commonsLangVersion}"

//    // embedded mongodb
    compile "de.flapdoodle.embed:de.flapdoodle.embed.mongo:${embedMongoVersion}"

    // jackson datatype
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

    // owasp
    compile "org.owasp.esapi:esapi:${owaspVersion}"

    // lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    // model mapper
    compile "org.modelmapper:modelmapper:${modelMapperVersion}"

    // webjars
    compile 'org.webjars:bootstrap:3.2.0'
    compile 'org.webjars:angularjs:1.2.23'
    compile 'org.webjars:angular-ui:0.4.0-3'
    compile 'org.webjars:angular-ui-bootstrap:0.11.0-2'
    compile 'org.webjars:angular-ui-router:0.2.11'
    compile 'org.webjars:jquery:2.1.1'
    compile 'org.webjars.npm:ngstorage:0.3.10'


    // unit testing
    testCompile 'org.slf4j:jcl-over-slf4j:1.7.12'
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "com.jayway.jsonpath:json-path"
    testCompile "com.jayway.jsonpath:json-path-assert"
    testCompile "com.jayway.restassured:spring-mock-mvc:${restAssuredVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.spockframework:spock-spring:1.0-groovy-2.4"
}

bootRun {
    addResources = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    def waitForScript = file('src/main/docker/wait-for-it.sh')
    doFirst {
        copy {
            from jar
            into stageDir
        }
        copy {
            from waitForScript
            into stageDir
        }
    }
}

task initTest(type: NpmTask) {
    args = ['run', 'init-test']

}

// javascript unit testing
task testUnit(type: NpmTask) {
    args = ['run', 'test-unit']

}

////include js unit tests into project build lifecycle
//test.dependsOn testUnit
